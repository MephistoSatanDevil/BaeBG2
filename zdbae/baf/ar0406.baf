IF
  Global("ZDBAEExists","GLOBAL",0)
THEN
RESPONSE #100
  SetGlobal("ZDBAEExists","GLOBAL",1)
  CreateCreature("ZDBAE",[2340.1650],5)
  Continue()
END

IF
  Global("ZDBAE_REVEAL","GLOBAL",1)
THEN
RESPONSE #100
  SetGlobal("ZDBAE_REVEAL","GLOBAL",2)
  SmallWait(25)
  ClearAllActions()
  StartCutSceneMode()
  StartCutScene("zdbcutr")
END

// Cutscene breakable 1
IF
  Global("ZDBAE1_CUTSCENE_BREAKABLE","GLOBAL",1)
THEN
RESPONSE #100
  SetCutSceneBreakable(FALSE)
  SetGlobal("ZDBAE1_CUTSCENE_BREAKABLE","GLOBAL",0)
  FadeToColor([50.0],0)
  // Create guards on location
  CreateCreature("copamb02",[2080.1520],SSE)
  ActionOverride("copamb02",Attack("ZDBAE"))
  CreateCreature("copamb02",[2108.1510],SSE)
  ActionOverride("copamb02",Attack("ZDBAE"))
  CreateCreature("copamb02",[2147.1484],SSE)
  ActionOverride("copamb02",Attack("ZDBAE"))
  SmallWait(5)
  EndCutSceneMode()
  FadeFromColor([50.0],0)
  AddJournalEntry(@101,INFO)
  ActionOverride("ZDBAE",StartDialogueNoSet([PC]))
END

// Cutscene breakable 2
IF
  Global("ZDBAE1_CUTSCENE_BREAKABLE","GLOBAL",2)
THEN
RESPONSE #100
  SetCutSceneBreakable(FALSE)
  SetGlobal("ZDBAE1_CUTSCENE_BREAKABLE","GLOBAL",0)
  FadeToColor([50.0],0)
  EndCutSceneMode()
  FadeFromColor([50.0],0)
  AddJournalEntry(@101,INFO)
  ActionOverride("ZDBAE",StartDialogueNoSet([PC]))
END

IF
  Global("ZDBAE_JOINED","GLOBAL",1)
  Global("ZDBAE_CROWD_HOSTILE","GLOBAL",0)
THEN
RESPONSE #100
  SetGlobal("ZDBAE_CROWD_HOSTILE","GLOBAL",1)
  ActionOverride("Nobleman",DestroySelf())
  ActionOverride("Nobleman",DestroySelf())
  ActionOverride("Announcer",DestroySelf())
END

IF
  Global("ZDBAE_BEGIN","GLOBAL",1)
  Global("ZDBAE_HOSTILE","GLOBAL",1)
  Global("ZDBAE_HOSTILE_DESTROY","GLOBAL",0)
THEN
RESPONSE #100
  SetGlobal("ZDBAE_HOSTILE_DESTROY","GLOBAL",1)
  ActionOverride("Nobleman",DestroySelf())
  ActionOverride("Nobleman",DestroySelf())
  ActionOverride("Announcer",DestroySelf())
END

// Destroy Baeloth if Hendak Released
IF
  Global("ZDBAEExists","GLOBAL",1)
  !Global("ZDBAE_BEGIN","GLOBAL",1)
  Global("SpawnFreedGladiators","AR0406",1)
  Global("ZDBAE_HOSTILE_DESTROY_SELF","GLOBAL",0)
THEN
RESPONSE #100
  SetGlobal("ZDBAE_HOSTILE_DESTROY_SELF","GLOBAL",1)
  ActionOverride("ZBAE",DestroySelf())
END
