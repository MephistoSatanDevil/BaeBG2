BEGIN ZDBAE

// Joining dialogue
IF ~!Global("ZDBAE_HOSTILE","GLOBAL",1) Global("ZDBAE_BEGIN","GLOBAL",0) Global("ZDBAE_SHUTUP","GLOBAL",0)~ THEN BEGIN ZDBAE1
  SAY @0
  IF ~~ THEN REPLY @1 DO ~SetGlobal("ZDBAE_BEGIN","GLOBAL",1)~ GOTO ZDBAE2
  IF ~Global("BA_BEGIN","GLOBAL",1)~ THEN REPLY @2 DO ~SetGlobal("ZDBAE_BEGIN","GLOBAL",1)~ GOTO ZDBAE3
  IF ~Global("BA_BEGIN","GLOBAL",1)~ THEN REPLY @3 DO ~SetGlobal("ZDBAE_BEGIN","GLOBAL",1)~ GOTO ZDBAE3
  IF ~~ THEN REPLY @4 DO ~SetGlobal("ZDBAE_BEGIN","GLOBAL",1)~ GOTO ZDBAE100
  IF ~~ THEN REPLY @5 DO ~SetGlobal("ZDBAE_BEGIN","GLOBAL",1) SetGlobal("ZDBAE_SHUTUP","GLOBAL",1)~ GOTO ZDBAEFU
END

IF ~!Global("ZDBAE_HOSTILE","GLOBAL",1) Global("ZDBAE_BEGIN","GLOBAL",1) Global("ZDBAE_SHUTUP","GLOBAL",1)~ THEN BEGIN ZDBAES
  SAY @6
  IF ~~ THEN REPLY @7 GOTO ZDBAE5
  IF ~~ THEN REPLY @8DO ~SetGlobal("ZDBAE_REVEAL","GLOBAL",1)~ GOTO ZDBAE100
  IF ~~ THEN REPLY @9 GOTO ZDBAEC
END

// Confused
IF ~~ BEGIN ZDBAEC
  SAY @10
  IF ~~ THEN EXIT
END

// Hello
IF ~~ THEN BEGIN ZDBAE2
  SAY @11
  IF ~~ THEN REPLY @12 GOTO ZDBAE5
  IF ~~ THEN REPLY @13 DO ~SetGlobal("ZDBAE_SHUTUP","GLOBAL",1)~ GOTO ZDBAEFU
END

IF ~~ THEN BEGIN ZDBAEFU
  SAY @14
  IF ~~ THEN EXIT
END

// Remember
IF ~~ THEN BEGIN ZDBAE3
  SAY@15
  IF ~Global("BPINBG","GLOBAL",1)~ THEN REPLY @16 GOTO ZDBAE4
  IF ~~ THEN REPLY @17 GOTO ZDBAE4
END

IF ~~ THEN BEGIN ZDBAE4
  SAY @18
  IF ~~ THEN REPLY @19 GOTO ZDBAE5
  IF ~~ THEN REPLY @20 GOTO ZDBAEFU
END

// Reject
IF ~~ THEN BEGIN ZDBAE5
  SAY @21
  IF ~~ THEN GOTO ZDBAE7
END

// Reveal
IF ~~ THEN BEGIN ZDBAE7
  SAY @22
  IF ~~ THEN DO ~SetGlobal("ZDBAE_BEGIN","GLOBAL",2) SetGlobal("ZDBAE_REVEAL","GLOBAL",1)~ EXIT
END

// Rescue
IF ~!Global("ZDBAE_HOSTILE","GLOBAL",1) Global("ZDBAE_REVEAL","GLOBAL",2)~ THEN BEGIN ZDBAE8
  SAY @23
  IF ~~ THEN REPLY @24 GOTO ZDBAE11
  IF ~~ THEN REPLY @25 GOTO ZDBAE100
  IF ~Global("BPINBG","GLOBAL",1)~ THEN REPLY @26 GOTO ZDBAE11
  IF ~Global("BA_ATTACKED","LOCALS",1)~ THEN REPLY @27 GOTO ZDBAE10
END

// Uncertain
IF ~~ THEN BEGIN ZDBAE10
  SAY@28
  IF ~~ THEN REPLY @26 GOTO ZDBAE11
  IF ~~ THEN REPLY @25 GOTO ZDBAE100
  IF ~Global("BPINBG","GLOBAL",1)~ THEN REPLY @29 GOTO ZDBAE100
END

// Joins
IF ~~ THEN BEGIN ZDBAE11
  SAY @30
  IF ~~ THEN DO ~SetGlobal("ZDBAE_JOINED","GLOBAL",1)~ EXIT
END

IF ~~ THEN BEGIN ZDBAE100
  SAY @31
  IF ~~ THEN DO ~
    SetGlobal("ZDBAE_HOSTILE","GLOBAL",1)
    ActionOverride("ZDBAE",ChangeAIScript("mage18c",CLASS))
    Enemy()~ EXIT
END

// Copper Coronet (AR0406)
// Response: If I may have your attention once again, gentlefolk of Athkatla! Lehtinan is proud to provide for your amusement once again! Bring your attention to the pit as we see two combatants fighting for their very lives!
INTERJECT ANNO1 0 ZDBAEINTRO0
  == ZDBAE IF ~~ THEN
  @32
END ANNO1 1
